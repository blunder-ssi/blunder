;;; Copyright 2023 The Plunder Authors
;;; Use of this source code is governed by a BSD-style license that can be
;;; found in the LICENSE file.

### demo_http_hello <- demo_full_tag_site

/+ boot
/+ io
/+ switch
/+ stew


""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

= (handleReq st [rid method path headers body])
# switch method
* GET
    , st
    @ entry (tabGet st path)
    | if (eql 0 entry)
        | [rid 404 b#{Not Found} [] b#{}]
    @ head | [(b#{Content-Type}, b#{text/plain})]
    | [rid 200 b#gotcha head entry]
* PUT
    @ st (tabIns path body st)
    @ resp [rid 201 b#done [] b#{}]
    | [st resp]
* _
    , st
    | [rid 400 b#bad [] b#{}]

!!=  | handleReq (tabIns b#"index.html" b#"<body />" #[])
     | [5 "GET" b#{index.html} [] b#{}]
  ++ (tabIns b#"index.html" b#"<body />" #[])
  ++ [5 200 b#gotcha [(b#{Content-Type}, b#{text/plain})] b#"<body />"]

!!=  | handleReq #[]
     | [5 "PUT" b#{/path} [] b#data]
  ++ (tabIns b#{/path} b#data #[])
  ++ [5 201 b#done [] b#{}]

= (httpHello st done)
: req <- syscall HTTP_HEAR
@ [st resp] (handleReq st req)
@ effect    (syscall | rowApply HTTP_ECHO resp)
| trk [=effect]
: _ <- effect
| httpHello st done

main=(runCog (httpHello #[]))
