;;; Copyright 2023 The Plunder Authors
;;; Use of this source code is governed by a BSD-style license that can be
;;; found in the LICENSE file.

### switch <- w64

"""
""" Atomic Switch
""" =============
"""

""" Imports """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

/+  boot


""" Switch Patterns """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""

= cenOneKid | "% literals must have one kid"
= cenWord   | "% literals expect a word (and no heir)"

= (readFanLit rex err ret)
| ifNot (isZero rexHeir-rex)
        | err rex "Unexpected heir"
| if (rexIsEmbd rex)
        | ret (rexEmbd rex)
| if (rexIsLeaf rex)
        | if (eql "WORD" (rexStyle rex))
                | readKey rex err ret
        | else
                | ret (rexText rex)
; Here, we know that the value is a node.
@ kids (rexKids rex)
@ ryn  (rexRune rex)
| if (eql ryn "%")
        @ [kid] kids
        | if (neq 1 len-kids)      | err rex cenOneKid
        | if (eql "," rexRune-kid) | err rex "TODO: Support cab and tab literals"
        | rexGetWord kid (err rex cenWord)
        & (text heir)
        | if (neq 0 heir) (err rex cenWord)
        | ret text
| if (eql ryn "#")
        | readBarLit rex err ret
| if (eql ryn ",")
        : vals  < forRow kids
                & (kid ret)
                | readFanLit kid err ret
        | ret vals
| else
        | err rex (strWeld "Unexpected rune: " ryn)

(try x)=(readFanLit x v2 v1)
!!= [%x]      | try `(%x)
!!= [[%x %y]] | try `['x' 'y']

= (readSwitchPat pat err cab lit)
| if (and (eql "_" rexText-pat) (eql %WORD rexStyle-pat))
        | cab
| readFanLit pat err lit

= rnNumb | "Invalid number literal"

!!= %_          | readSwitchPat `_        v2 {_} v1
!!= [%x]        | readSwitchPat `(%x)     v2 {_} v1
!!= [[%x]]      | readSwitchPat `[%x]     v2 {_} v1
!!= [[%x %y]]   | readSwitchPat `[%x 'y'] v2 {_} v1
!!= [%x]        | readSwitchPat `"x"      v2 {_} v1
!!= [3]         | readSwitchPat `3        v2 {_} v1
!!= [%a]        | readSwitchPat `a        v2 {_} v1


""" Parsing Switch Patterns """"""""""""""""""""""""""""""""""""""""""""""""""""

;
; This parses the generial pattern-match form with a sequence of = nodes,
; one per match.
;
; TODO: Come up with a better name for this.
;
; `parseAlien` is a parser to handle heirs that are not `=` nodes.
;
; See the tests for examples of what syntax is supported.
;
= (unrollSlip2Rune rune rex parseAlien err ok)
^ _ NIL rex
? (loop acc form)
: kids heir
        < rexGetNodeWithRune rune form
                : end <- parseAlien form err
                | ok (listToRowReversed (CONS end acc))
@ size (len kids)
| if (and (eql 1 size) (neq 0 heir))
        ; = foo
        ; body
        @ [kid] kids
        | ok (listToRowReversed (CONS [kid heir] acc))
| if (neq 2 size)
        | err form (strWeld rune " node expects two parameters")
; = foo body
@ acc (CONS kids acc)
| if (isZero heir)
        | ok (listToRowReversed acc)
| loop acc heir

unrollSlip2Tis=(unrollSlip2Rune '=')
unrollSlip2Tar=(unrollSlip2Rune '*')

foo=(x e o & o (varE "_", x))
(try x)=(unrollSlip2Tis x foo v2 id)

!!= [[`x `y] [`_ `9]]
        | try ` = x y
                9

!!= [[`x `y]]
        | try ` = x y

!!= [[`x `y]]
        | try ` = x
                y

!!= [[`3 `4] [`5 `6] [`_ `_]]
        | try ` = 3 4
                = 5 6
                = _ _


""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

; TODO: Make the value-parser configurable

; TODO: readSwitchExp should produce a maybe for the fallback

> (Rex > Read exp)
> Rex
> Read (exp, Tab Any exp)
= (readSwitchPats readExp rex err ok)
: defaultFallback
    < readExp `0 err
: clauses
    < ^ unrollSlip2Tar rex _ err
      ? (onNonMatchingTail x err ret)
      | ret (varE "_", x)
: cases
    < forRow clauses
    & ([key exp] ret)
    : exp < readExp exp err
    : nat < & pure
            | readSwitchPat key err
            * pure NONE
            * compose pure SOME
    | ret [nat exp]
^ ok (foldl _ (defaultFallback, #[]) cases)
& ([wild acc] [mKey exp])
: key <- **maybeCase mKey [exp acc]
| [wild (tabPut acc key exp)]

= (readSwitchExp readExpr rex err ok)
: _ exp [fallback arms]
  < ^ rexParseKids rex _ err
    | (readRex, readExpr, readSwitchPats readExpr)
| ok exp fallback arms

!!= [`0 [3=(`4) 5=(`6)]]
        | readSwitchPats readRex `((* 3 4)(* 5 6)) v2 id

!!= [`7 [3=(`4) 5=(`6)]]
        | readSwitchPats readRex `((* 3 4)(* 5 6)7) v2 id

!!= [`x [3=(`4) 5=(`6)]]
        | readSwitchPats readRex `((* 3 4)(* 5 6)(x)) v2 id

!!= [`x (tabIns [3 3] `6 [3=(`4)])]
        | readSwitchPats readRex `((* 3 4)(* [3 3] 6)(x)) v2 id


""" Atomic Switch """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

= (switchE expr wild pats)
@ canUseRowIndex
        | listAnd
        | listZipWith eql (listEnumFrom 0)
        | cabToList (tabKeys pats)
| if canUseRowIndex
        | bopE (cnsE switch,    expr, wild, bowE (tabVals pats))
| else
        | bopE (cnsE tabSwitch, expr, wild, tabE pats)

= ({#switch} s p e a r err ok)
: exp fallback arms <- readSwitchExp readRex r err
| ok p a
| switchE exp fallback arms


""" Tests """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

= (switchTest x)
# switch x
* 346    b#346
* 345    b#345
* derp   b#dur
* %hi    b#hi
* "yop"  b#yop
| b#hur

!! eql b#346 switchTest-346
!! eql b#345 switchTest-345
!! eql b#hi  switchTest-(%hi)
!! eql b#yop switchTest-"yop"
!! eql b#hur switchTest-45
!! eql b#dur switchTest-"derp"

= (switchTest2 x)
# switch x
* _ %Other
* 0 %Zero
* 1 %One
* 2 %Two
* 3
| barNat b#Three

!! eql %Three switchTest2-3
!! eql %Two   switchTest2-2
!! eql %Other switchTest2-9

= (switchTest3 x)
# switch x
* 1              | %One
* [0 0]          | %ZeroZero
* [%a 'b']       | %AyeBee
* [c d]          | %CeeDee
* ['GET' b#{/p}] | %GetPee
* [b#a]          | %AyeBar
* [[[%a]]]       | %Ayyye
* 9              | %Nine
%Fallback

!! eql %One      | switchTest3 1
!! eql %ZeroZero | switchTest3 [0 0]
!! eql %AyeBee   | switchTest3 [{a} %b]
!! eql %CeeDee   | switchTest3 [{c} {d}]
!! eql %GetPee   | switchTest3 ['GET' b#{/p}]
!! eql %AyeBar   | switchTest3 [b#a]
!! eql %Ayyye    | switchTest3 [[[%a]]]
!! eql %Nine     | switchTest3 9
!! eql %Fallback | switchTest3 8


""" Exports """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

^-^ unrollSlip2Rune
^-^ unrollSlip2Tis
^-^ unrollSlip2Tar
^-^ readSwitchExp
^-^ switchE
^-^ "#switch"
