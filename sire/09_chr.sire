; Copyright 2023 The Plunder Authors
; Use of this source code is governed by a BSD-style license that can be
; found in the LICENSE file.

### 09_chr <- 08_sci

;;;; Characters
;;;; ==========
;;;;
;;;; Operations on ASCII characters


;;; Imports ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

/+  01_fan  []
/+  02_bit  [if and or not]
/+  03_nat  [add sub]
/+  04_cmp  [eql gte lte lth]
/+  05_row  []
/+  06_rex  []
/+  07_dat  [all {,}]
/+  08_sci  []


;;; Definitions ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(isDigit c)=(and (gte c {0}) (lte c {9}))
(isUpper c)=(and (gte c {A}) (lte c {Z}))
(isLower c)=(and (gte c {a}) (lte c {z}))
(isAlpha c)=(or isUpper-c isLower-c)
(isPrint c)=(and (gte c { }) (lte c {~}))

= (isHexDigit c)
| or (isDigit c)
| or (and (gte c {A}) (lte c {F}))
     (and (gte c {a}) (lte c {f}))

(isAlphaNum c)=(or isAlpha-c isDigit-c)

(toLower c)=(if (isUpper c) (add 32 c) c)
(toUpper c)=(if (isLower c) (sub c 32) c)

(chr c)=(add c 48)
(ord c)=(sub c 48)

newlineChar=10
tabChar=9
spaceChar=32


;;; Tests ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

!!= 1 | eql 1   (ord {1})
!!= 1 | eql {3} (chr 3)
!!= 1 | eql 5   (ord (chr 5))
!!= 1 | eql {9} (chr (ord {9}))

!!= 1 | isLower {c}
!!= 1 | isUpper {C}
!!= 1 | not (isAlpha {0})
!!= 1 | isDigit {0}
!!= 1 | not (isDigit {a})

!!= 1 | all isHexDigit [{0} {9} {a} {A} {f} {F}]
!!= 1 | not (isHexDigit {g})

!!= 1 (isPrint {a})
!!= 0 (isPrint 1)


;;; Exports ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

^-^
^-^ ord chr
^-^ isDigit isHexDigit
^-^ isUpper isLower isAlpha isPrint
^-^ isAlphaNum
^-^ toLower toUpper
^-^
^-^ newlineChar tabChar spaceChar
^-^
