;;; Copyright 2023 The Plunder Authors
;;; Use of this source code is governed by a BSD-style license that can be
;;; found in the LICENSE file.

### 19_pat <- 18_sug

"""
""" Pattern Matching
""" ================
"""
""" Extends `@` to support destructuring on:
"""
""" - Rows
""" - Tabs
""" - Pins
""" - Laws
"""
""" Also supports strictness annotations on bindings.
"""
"""
""" TODOs
""" -----
"""
""" -   TODO: Don't output `_` bindings.
"""
""" -   TODO: Don't output trivial rebindings (x=y).
"""
""" -   TODO: Implement LAW/PIN patterns in a principled way.
"""
"""     We should register some sort of "destructuror" against the PIN /
"""     LAW bindings in `01_fan.sire`. You should be able to shaddow
"""     (or not import) these like anything else.
"""


""" Imports """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

/+  01_fan
/+  02_bit  [if else ifNot not and or FALSE]
/+  03_nat  [add div gteNat inc lthNat isZero]
/+  04_app  [len null idx]
/+  05_eql  [eql neq]
/+  06_cow  [weld map turn foldl foldr v1 v2 v4]
/+  07_rex  [rexKids rexRune rexHeir rexSons rexText rexVal rexEql "`"]
/+  07_rex  [rexIsNode rexIsLeaf]
/+  07_rex  [rexGetKidsOfNodeWithRune rexGetWord]
/+  07_rex  [rexGetNodeWithRune]
/+  07_rex  [rexSetRune rexSetSons rexSetHeir]
/+  07_rex  [NEST OPEN WORD]
/+  07_rex  [rexIdnt]
/+  08_dat
/+  09_sci  []
/+  10_chr  []
/+  11_str  [strWeld strCat]
/+  12_cab  [readKey]
/+  13_tab  ["%" "%%" tabToPairs tabFromPairs tabIdx tabToList tabMut]
/+  13_tab  [isTabSugar desugarTab]
/+  14_exp
/+  15_hax  ['#']
/+  16_pad  []
/+  17_bar  []
/+  18_sug  [{``} {:} {~} {::} {~~} {==}]


""" Pattern Matching On Rows """""""""""""""""""""""""""""""""""""""""""""""""""

; TODO: Rebuild this as a call to (sequenceBinds >=> bindSeqE).

= (destructureRowE tmpVar lhs rowExpr bodyExpr)
@ isVar  | rexEql rowExpr varE-(rexText rowExpr)
@ tmpVar | if isVar (rexText rowExpr) tmpVar
@ go
        ? (go binds)
        : bind binds <- **listCase binds bodyExpr
        @ bindVar (idx 1 bind)
        | if (eql '_' bindVar) (go binds)
        | letE bindVar
                | appE
               ,, cnsE (idx (idx 0 bind))
               ,, varE tmpVar
        | go binds
| if isVar id (letE tmpVar rowExpr)
| go (listFromRow (tag lhs))

!!= (destructureRowE %_g1 [%x %y %z] `(a b) (appE [`x `y]))
       ``
       #@ _g1 (a b)
       #@ x (#| $(idx 0) _g1)
       #@ y (#| $(idx 1) _g1)
       #@ z (#| $(idx 2) _g1)
        (#| x y)

!!= (destructureRowE %_g1 [%x %y %z] `yo (appE [`x `y]))
       ``
       #@ x (#| $(idx 0) yo)
       #@ y (#| $(idx 1) yo)
       #@ z (#| $(idx 2) yo)
        (#| x y)


""" Parsing Binders in General """""""""""""""""""""""""""""""""""""""""""""""""

; # datatype Pat
; * VAR=v k:Nat n:Symb         ;;   x
; * ALI=a k:Nat n:Symb p:Pat   ;;   x@p
; * SEQ=s x:Pat                ;;   !x
; * TAB=t x:(Tab Val Pat)      ;;   %[a=b c=d]
; * ROW=r x:(Row Pat)          ;;   [a b c]
;
; First parse patterns into data structures.
;
;   @ [x [y z] %[a=b c=d e=f]] (foo foo)
;   body
;
;     =>
;
;   | ROW
;  ,, VAR 1 'x'
;  ,, ROW
;      ,, VAR 2 'y'
;      ,, VAR 3 'z'
;  ,, TAB
;      %% =a (VAR 4 'b')
;      %% =c (VAR 5 'd')
;      %% =e (VAR 6 'f')
;
; Then generate code from that:
;
; #@ _g0  (foo foo)
; #@ x    (idx-0      _g0)
; #@ _g1  (idx-1      _g0)
; #@ y    (idx-0      _g1)
; #@ z    (idx-1      _g1)
; #@ _g2  (idx-2      _g0)
; #@ b    (tabIdx-"a" _g2)
; #@ d    (tabIdx-"c" _g2)
; #@ f    (tabIdx-"e" _g2)
;  body
;
; This same process should work more-or-less verbatim in other contexts:
; functions, lambdas, etc.
;

= sbBadForm | "seq-patterns must be a single banged symbol: !x"
= sbBadHeir | "seq-patterns may not have an heir"

= (parseSeqBinder rex err ok)
@ sons | rexSons rex
@ expr | idx 0 sons
| if (not (rexIsNode rex))   | err rex sbBadForm
| if (neq (rexRune rex) "!") | err rex sbBadForm
| if (neq (len sons) 1)      | err rex sbBadForm
| if (neq (rexHeir rex) 0)   | err rex sbBadHeir
: sym <- readSymbol expr err
| ok [%s (rexIdnt expr) sym]

= rbBadForm | "Row-Patterns must be a row of symbols [x y z]"
= rbBadHeir | "Row-Patterns may not have an heir"

= (parseRowBinder parseBinder rex err ok)
| if (not (rexIsNode rex))   | err rex rbBadForm
| if (neq (rexRune rex) ",") | err rex rbBadForm
| if (neq (rexHeir rex) 0)   | err rex rbBadHeir
: elems <- traverseRow (son ret & parseBinder son err ret) (rexSons rex)
| ok [%r elems]

= noHeir | "Unexpected heir"

= (parseTabBind parseBinder rex err ok)
: kids <- rexGetKidsOfNodeWithRune "=" rex
        | err rex "Expected an = node"
| if (neq 0 (rexHeir rex))
        | err (rexHeir rex) noHeir
| if (eql 1 len-kids)
        @ rex (idx 0 kids)
        : k <- readKey rex err
        @ idnt (rexIdnt rex)
        | ok k [%v idnt k]
| if (eql 2 len-kids)
        : k <- readKey (idx 0 kids) err
        : v <- parseBinder (idx 1 kids) err
        | ok k v
| else
        | err rex "Tab-Binder must have form (= k) or (= k v)"

= (parseTabBinder parseBinder rex err ok)
: kids <- readNodeWithoutHeirButWithRune "%" rex err
| if (neq 1 len-kids)
        | err rex "Tab-Pattern expects % rune with one kid"
@ com (idx 0 kids)
: comKids <- readKidsOfNodeWithRune "," com err
: result  <- ^ traverseRow _ comKids
             ? (step kid ret)
             : k v <- parseTabBind parseBinder kid err
             | ret [k v]
| ok (%t, tabFromPairs result)

; @x
; x@[a b c]
; TODO: support x@y@z, just for completeness (zero, one, or many)
= (parseAliasedBinder parseBinder rex err ok)
: kids <- readNodeWithoutHeirButWithRune "@" rex err
| if (eql 1 len-kids)
        | parseBinder (idx 0 kids) err ok
| if (neq 2 len-kids)
        | err rex "Alias-Patterns expect two kids: x=y"
: foo < parseBinder (idx 0 kids) err
: bar < parseBinder (idx 1 kids) err
| ok [%a foo bar]


; =(PIN x)
= (parsePinBinder parseBinder rex err ok)
: kids <- readNodeWithoutHeirButWithRune "|" rex err
| if (neq 2 len-kids)
        | err rex "Pin-Pattern expects two kids: (PIN x)"
| ifNot (rexEql `PIN | idx 0 kids)
        | err rex "Pin-Pattern expects rex-head to be `PIN"
: item < parseBinder (idx 1 kids) err
| ok [%pin item]


; @(LAW n a b)
= (parseLawBinder parseBinder rex err ok)
: kids <- readNodeWithoutHeirButWithRune "|" rex err
| if (neq 4 len-kids)
        | err rex "Law-Pattern expects four kids: (LAW n a b)"
| ifNot (rexEql `LAW | idx 0 kids)
        | err rex "Law-Pattern expects rex-head to be `LAW"
: n < parseBinder (idx 1 kids) err
: a < parseBinder (idx 2 kids) err
: b < parseBinder (idx 3 kids) err
| ok [%law n a b]

= (parseBinder rex err ok)
| if (rexIsLeaf rex)
        : sym <- readSymbol rex err
        | ok [%v (rexIdnt rex) sym]
@ rune (rexRune rex)
| if (eql rune ".")
        : sym <- readSymbol rex err
        | ok [%v (rexIdnt rex) sym]
| if (eql rune "!")
        | parseSeqBinder rex err ok
| if (eql rune ",")
        | if (isTabSugar rex)
            | parseBinder (desugarTab rex) err ok
        | parseRowBinder parseBinder rex err ok
| if (eql rune "%")
        | parseTabBinder parseBinder rex err ok
| if (eql rune "@")
        | parseAliasedBinder parseBinder rex err ok
| if (eql rune "|")
        ;
        ; TODO: Gnarly hack, don't keep
        ;
        ; This just makes any unknown constror pattern works as if it
        ; was a row.  This mostly works but is extremely jank.  The right
        ; way involves looking up meta-information to verify that this
        ; is a #record, checking the arity, etc.
        ;
        @ sons | rexSons rex
        @ numb | len sons
        @ head | idx 0 sons
        | if (rexEql head `LAW)
            | parseLawBinder parseBinder rex err ok
        | if (rexEql head `PIN)
            | parsePinBinder parseBinder rex err ok
        ^ parseBinder _ err ok
        | rexSetRune ","
        | rexSetSons (drop 1 | rexSons rex)
        | rex
| else
        | err rex "Unknown binding pattern"

!!= (parseBinder `[x y z] v2 id)
       ,, %r
       ,, ,, [%v 0 %x]
          ,, [%v 0 %y]
          ,, [%v 0 %z]

!!= (parseBinder `[x y !z] v2 id)
       ,, %r
       ,, ,, [%v 0 %x]
          ,, [%v 0 %y]
          ,, [%s 0 %z]

!!= (parseBinder `(%[=x y=b]) v2 id)
       ,, %t
       ,, %% =x [%v 0 %x]
          %% =y [%v 0 %b]

!!= (parseBinder `[=x y=b] v2 id)
       ,, %t
       ,, %% =x [%v 0 %x]
          %% =y [%v 0 %b]

!!= (parseBinder `[%[=x] [y]] v2 id)
       ,, %r
       ,, ,, [%t %[x=[%v 0 %x]]]
          ,, [%r [[%v 0 %y]]]


!!= (parseBinder `(foo@[%[=x] [y]]) v2 id)
       ,, 'a'
       ,, ['v' 0 'foo']
       ,,  ,, 'r'
           ,,  ,, ['t' %[x=['v' 0 'x']]]
               ,, ['r' [['v' 0 'y']]]


!!= (parseBinder ``[$$(varKE 1 %x) $$(varKE 2 %y)] v2 id)
       ,, %r
       ,, ,, [%v 1 %x]
          ,, [%v 2 %y]


""" Sequence Binds """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

;
; Then, generate a binder sequence from this:
;
;   @ [x [y z] %[a=b c=d e=f]] (foo foo)
;   body
;
;     =>
;
; ~~ [ "x"    `(idx-0      _g0) ]
; ~~ [ "_g1"  `(idx-1      _g0) ]
; ~~ [ "y"    `(idx-0      _g1) ]
; ~~ [ "z"    `(idx-1      _g1) ]
; ~~ [ "_g2"  `(idx-2      _g0) ]
; ~~ [ "b"    `(tabIdx-"a" _g2) ]
; ~~ [ "d"    `(tabIdx-"c" _g2) ]
; ~~ [ "f"    `(tabIdx-"e" _g2) ]
;

= (sequenceBindPair nex bindSpec exp)
^ _ exp bindSpec nex NIL
        & (nex acc)
        @ row (listToRowReversed acc)
        | [nex (map (take 3) row) (cat | map idx-3 row)]
? (go exp bindSpec nex acc ok)
@ typ (idx 0 bindSpec)
| if (eql 'v' typ)
        @ key (idx 1 bindSpec)
        @ var (idx 2 bindSpec)
        | ok nex [key var exp []]::acc
| if (eql 's' typ)
        @ key (idx 1 bindSpec)
        @ var (idx 2 bindSpec)
        | ok nex [key var exp [var]]::acc
@ tey nex
@ tmp (gensym tey)
@ nex (inc nex)
| if (eql 'a' typ)
        @ acc [tey tmp exp []]::acc
        : nex acc <- go (varE tmp) (idx 1 bindSpec) nex acc
        : nex acc <- go (varE tmp) (idx 2 bindSpec) nex acc
        | ok nex acc
| if (eql 'pin' typ)
        @ acc [tey tmp exp []]::acc
        @ exp (appE (cnsE pinItem, varE tmp))
        : nex acc <- go exp (idx 1 bindSpec) nex acc
        | ok nex acc
| if (eql 'law' typ)
        @ acc [tey tmp exp []]::acc
        : nex acc <- go appE|(cnsE lawName, varE tmp) (idx 1 bindSpec) nex acc
        : nex acc <- go appE|(cnsE lawArgs, varE tmp) (idx 2 bindSpec) nex acc
        : nex acc <- go appE|(cnsE lawBody, varE tmp) (idx 3 bindSpec) nex acc
        | ok nex acc
@ dat (idx 1 bindSpec)
@ acc [tey tmp exp []]::acc
@ res
        | if (eql 't' typ)
                ^ foldl _ [nex acc] (tabToPairs dat)
                & (st elem)
                @ nex (idx 0 st)
                @ acc (idx 1 st)
                @ key (idx 0 elem)
                @ pat (idx 1 elem)
                @ exp (appE (cnsE tabIdx-key, varE tmp))
                : nex acc <- go exp pat nex acc
                | [nex acc]
        | if (eql 'r' typ)
                ^ foldl _ [nex acc] (tag dat)
                & (st elem)
                @ nex (idx 0 st)
                @ acc (idx 1 st)
                @ i   (idx 0 elem)
                @ pat (idx 1 elem)
                @ exp (appE (cnsE idx-i, varE tmp))
                : nex acc <- go exp pat nex acc
                | [nex acc]
        | else
            | die ['bad bind spec' bindSpec]
@ nex (idx 0 res)
@ acc (idx 1 res)
| ok nex acc

= (sequenceBinds initialAft bindSpecs)
^ foldl _ initialAft,[],[] bindSpecs
& (acc item)
;
@ aft   (idx 0 acc)
@ soFar (idx 1 acc)
@ deps  (idx 2 acc)
;
@ bind  (idx 0 item)
@ exp   (idx 1 item)
;
@ res    (sequenceBindPair aft bind exp)
@ aft    (idx 0 res)
@ extra  (idx 1 res)
@ depend (idx 2 res)
;
| [aft (weld soFar extra) (weld deps depend)]

!!=    ,, 5
       ,, [[4 %y (`x)]]
       ,, []
    | sequenceBinds 5 [[[%v 4 %y] `x]]

!!=    ,, 5
       ,, [[4 %y (`x)]]
       ,, ['y']
    | sequenceBinds 5 [[[%s 4 %y] `x]]

!!=    ,, 8
       ,,  ,, [5 '_g5'  ``(foo foo)]
           ,, [6 '_g6'  ``(#| $(idx 0) _g5)]
           ,, [1 %x     ``(#| $(idx 0) _g6)]
           ,, [7 '_g7'  ``(#| $(idx 1) _g5)]
           ,, [2 %y     ``(#| $(idx 0) _g7)]
       ,,  [%x %y]
    | sequenceBinds 5
    | v1
   ,,   | v2 %r
       ,, [%r [[%s 1 %x]]]
       ,, [%r [[%s 2 %y]]]
   ,, `(foo foo)

!!=
       ,, 8
       ,,  ,, [5 '_g5'  ``(foo foo)]
           ,, [6 '_g6'  ``(#| $(idx 0) _g5)]
           ,, [2 %x     ``(#| $(tabIdx 'x') _g6)]
           ,, [7 '_g7'  ``(#| $(idx 1) _g5)]
           ,, [3 %y     ``(#| $(idx 0) _g7)]
       ,, []
    | sequenceBinds 5
    | v1
   ,,   | v2 %r
       ,, [%t %[x=[%v 2 %x]]]
       ,, [%r [[%v 3 %y]]]
   ,, `(foo foo)


""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

= (bindSeqE binds body deps)
@ addBind
        & (bind body)
        @ key (idx 0 bind)
        @ var (idx 1 bind)
        @ exp (idx 2 bind)
        @ sameVar (readSymbol exp (_ _ & FALSE) (eql var))
        | if (or (eql var '_') sameVar) body
        | letKE key var exp body
@ addDep
        & (dep body)
        | bapE (cnsE seq, varE dep, body)
@ depBody
    | foldr addDep body deps
| foldr addBind depBody binds

!!=   ``
       #@ $$(varKE 5 "_g5") (foo foo)
       #@ $$(varKE 6 "_g6") (#| $(idx 1)     _g5)
       #@ $$(varKE 1 "y")   (#| $(idx 0)     _g6)
       #@ $$(varKE 7 "_g7") (#| $(idx 0)     _g5)
       #@ $$(varKE 2 "x")   (#| $(tabIdx %x) _g7)
       #| $seq $$(varE %_g5)
       #| $seq $$(varE %y)
       #| $seq $$(varE %z)
        x
   | bindSeqE
          ,, [5 '_g5'  ``(foo foo)]
          ,, [6 '_g6'  ``(#| $(idx 1) _g5)]
          ,, [1 %y     ``(#| $(idx 0) _g6)]
          ,, [7 '_g7'  ``(#| $(idx 0) _g5)]
          ,, [2 %x     ``(#| $(tabIdx 'x') _g7)]
       | `x
   | [%_g5 %y %z]


""" Multi Binders """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

= (isMultiBinder rex)
| eql "=" (rexRune rex)

= mbNeedsTwo | "Each multi-bind needs at least two son"

= (readMultiBinder rex err ok)
| if (isZero rex) ok-[]
: sons heir <- readNodeWithRune "=" rex err
| if (lthNat len-sons 2) (err rex mbNeedsTwo)
: bind < parseBinder (idx 0 sons) err
: more < readMultiBinder heir err
@ exp | appE (drop 1 sons)
| ok
| rowCons [bind exp] more

!!= (readMultiBinder `((= x 3)(= y 3 4)) v2 id)
        [[[%v 0 %x] `3] [[%v 0 %y] `(#| 3 4)]]

= (multiBindE binds bodyExpr)
@ f     & (bind body)
        | letE (idx 0 bind) (idx 1 bind) body
| foldr f bodyExpr binds


""" Lambda Binders """""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

= (isLambdaBinder rex)
| and (rexIsNode rex)
| and (eql "|" (rexRune rex))
| and (isZero (rexHeir rex))
@ sons (rexSons rex)
| and (gteNat (len sons) 2)
| rexIsSymbol (idx 0 sons)

!!= 1 | isLambdaBinder `(f x)
!!= 0 | isLambdaBinder `f
!!= 0 | isLambdaBinder `(f)
!!= 0 | isLambdaBinder `(| f)
!!= 1 | isLambdaBinder `(f a b c)
!!= 0 | isLambdaBinder `[]


""" Pattern Matching on Tabs """""""""""""""""""""""""""""""""""""""""""""""""""

; TODO: Rebuild this as a call to (sequenceBinds >=> bindSeqE).
= (destructureTabE nex bindings tabExp bodyExp ok)
@ tmp (gensym nex)
@ go
        ? (go bind)
        | **listCase bind
                | bodyExp
        & (kv more)
        @ key (idx 0 kv)
        @ val (idx 1 kv)
        | letE val
                | appE (cnsE tabIdx, cnsE key, varE tmp)
        | go more
| ok (inc nex)
| letE tmp tabExp
| (go (tabToList bindings))

!!= (destructureTabE 9 %[x=(%y) p=(%q)] `tabby `(add y q) v2)
        | v2 10
       ``
       #@ _g9 tabby
       #@ q (#| $tabIdx $$(cnsE %p) _g9)
       #@ y (#| $tabIdx $$(cnsE %x) _g9)
        (add y q)

!!= (destructureTabE 9 %[] `tabby `(add y q) v2)
        | v2 10
       ``
       #@ _g9 tabby
        (add y q)


""" Read Signatures with Support for Destructuring """""""""""""""""""""""""""""

= sigExpect | "Expected `x` or signature `(x y z)`"
= sigNoHeir | "name-sequence cannot have an heir"

= (readBindSig rex err ok)
@ rune (rexRune rex)
| if (eql rune "|")
    | if (neq 0 (rexHeir rex))
        | err rex sigNoHeir
    @ sons (rexSons rex)
    | if (isZero len-sons)
        | err rex sigExpect
    ^ traverseRow _ sons ok
    ? (step son ret)
    | parseBinder son err ret
| else
    : bin <- parseBinder rex err
    | ok [bin]

!!= ,[[%v 0 %a] [%v 0 %b] [%v 0 %c]]
        | readBindSig `(a b c) v2 v1

!!= [[[%r [[%v 0 %x] [%s 0 %a]]] [%v 0 %y] [%s 0 %z]]]
        | readBindSig `([x !a] y !z) v2 v1

= symSigExpect | "Expected `x` or signature `(x y z)`"
= symSigNoHeir | "name-sequence cannot have an heir"

;
;
; `readDestroyer` parsing something with the same shape as a function
; signature:
;
;     (| Key Pat*)
;
> Rex > RexParser2 Key (Row Pat)
= (readDestroyer rex err ok)
: sons heir
        < rexGetNodeWithRune "|" rex
        | err rex symSigExpect
| ifNot (isZero heir)
        | err rex symSigNoHeir
| if (lthNat len-sons 2)
        | err rex symSigExpect
: sym
        < readKey (idx 0 sons) err
: binds <- ^ traverseRow _ (drop 1 sons)
           ? (step son ret)
           | parseBinder son err ret
| ok sym binds


(tryRBSS x)=(readDestroyer x v2 v2)

!!= [%a [[%v 0 %b] [%v 0 %c]]]                  | tryRBSS `(a b c)
!!= [%a [[%v 0 %b] [%r [[%v 0 %c] [%v 0 %d]]]]] | tryRBSS `(a b [c d])
!!= [`a sigExpect]                              | tryRBSS `(a)
!!= [`a sigExpect]                              | tryRBSS `a
!!= [`[a b] sigExpect]                          | tryRBSS `[a b]
!!= [`([][]) sigExpect]                         | tryRBSS `([][])
!!= [`((|)[]) sigNoHeir]                        | tryRBSS `((|)[])



""" Generic Pattern Matching """""""""""""""""""""""""""""""""""""""""""""""""""

= (makeDestroyer aft binds bodyExpr pure)
: aft sig
        < forRowWithState aft binds
        & (aft bind ret)
        | if (eql %v (idx 0 bind))
                | ret aft (idx 2 bind)
        | ret (inc aft) (gensym aft)
@ binder (zip binds (map varE sig))
@ res  (sequenceBinds aft binder)
@ aft  (idx 0 res)
@ bSeq (idx 1 res)
@ deps (idx 2 res)
; if (not null-deps) (die [['sequenceBinds' 'returned' res] [aft binder]])
| pure aft sig
| bindSeqE bSeq bodyExpr deps

= ("@" s p e a r err ok)
@ kids (rexKids r)
| if (eql 2 len-kids)
        @ binders  (idx 0 kids)
        @ bodyExpr (idx 1 kids)
        : binds <- readMultiBinder binders err
        @ res  (sequenceBinds a binds)
        @ a    (idx 0 res)
        @ bSeq (idx 1 res)
        @ deps (idx 2 res)
        | ok p a
        | bindSeqE bSeq bodyExpr deps
| if (neq 3 len-kids)
        | err r "@ requires 2 or 3 kids"
@ binder    (idx 0 kids)
@ valueExpr (idx 1 kids)
@ bodyExpr  (idx 2 kids)
| if (isLambdaBinder binder)
        : sym binds       <- readDestroyer binder err
        : a sig valueExpr <- makeDestroyer a binds valueExpr
        | ok p a
        | letE sym
                | wutE (rowCons sym sig) valueExpr
        | bodyExpr
: bindSpec <- parseBinder binder err
@ res  (sequenceBinds a [[bindSpec valueExpr]])
@ a    (idx 0 res)
@ bSeq (idx 1 res)
@ deps (idx 2 res)
| ok p a
| bindSeqE bSeq bodyExpr deps

!!= 7
       @ [x y z] [3 4]
       | (add x (add y z))

!!= 7
       @ [!x !y !z] [3 4]
       | (add x (add y z))

!!= 7
       @ %[=x =y =z] %[y=4 z=3]
       | (add x (add y z))

!!= 7
       @ %[x=(!x) =y =z] %[y=4 z=3]
       | (add x (add y z))

!!= [5 3 0 3 4 3]
        @ t       | 3
        @ [x y z] | [t 4 5]
        | [z x 0 x y x]

!!= [[0 3 5 4 0] 3 4]
        @ f 5
        @ [p q r s]  [3 4 f]
        @ %[x=a =y]  %[x=3 y=4]
        | [[s p r q s] a y]

!!= 5
        @ (addTwo x) (add x 2)
        | addTwo 3

!!= 5
        @ (uncurry f [x y]) (f x y)
        | uncurry add [2 3]

!!= [4 3]
        @   = x     | 3
            = y     | x
            = z     | [x (inc y)]
            = [a b] | z
        [b a]

!!= [500 200 1001]
        @ %[pos=[px py pz] vel=[vx vy vz]]
               %% =pos [500 200 1]
               %% =vel [0 0 1000]
       ,, add px vx
       ,, add py vy
       ,, add pz vz

!!=  [11 22 33]
    @ (addV3 [x1 y1 z1] [x2 y2 z2])
        ,, add x1 x2
        ,, add y1 y2
        ,, add z1 z2
    | addV3 [1 2 3] [10 20 30]


""" Named Lambdas with Destroy """""""""""""""""""""""""""""""""""""""""""""""""

; TODO: Lambda binders should have binder-identities

; TODO: This is lacking features from the base syntax (**)/etc.

= (readNamedLambdaSig rex err ok)
| if rexIsNode-rex
    | readDestroyer rex err ok
: sym <- readSymbol rex err
| ok sym []

;
= ("?" s p e a r err ok)
: sigRex bodyExpr <- rexParseKids r [readAny readAny] err
: sym    binds    <- readNamedLambdaSig sigRex err
: a sig  bodyExpr <- makeDestroyer a binds bodyExpr
| ok p a
| wutE (rowCons sym sig)
| bodyExpr

!!= 5 | (uncurry f [x y] ? f x y) add [2 3]

!!= ~[0 0 0] | listTake 3 z?(0::z)


""" Anon Lambdas with Destroy """"""""""""""""""""""""""""""""""""""""""""""""""

= ("&" s p e a r err ok)
: sigRex bodyExpr <- rexParseKids r [readAny readAny] err
: binds           <- readBindSig sigRex err
: a sig  bodyExpr <- makeDestroyer a binds bodyExpr
| ok p a
| lamE sig
| bodyExpr

!!= 5 | [x y]&(add x y) [2 3]

!!= 5 | (f [x y] & f x y) add [2 3]

!!= %[x=0 y=1 z=2]
     | foldr ([k v] t & tabMut k v t) %[] [[%x 0] [%y 1] [%z 2]]


""" Top-Level Definitions with Destroy """""""""""""""""""""""""""""""""""""""""

= (keyE k)
| if (strIsSymbol k) (varE k) (txtE k)

= (sogE f rs)
| (NEST 0 "|" (rowCons (keyE f) (map varE rs)) 0)

= (keyKE i k)
| if (strIsSymbol k) (varKE i k) (txtKE i k)

= (sogKE k f rs)
| (NEST 0 "|" (rowCons (keyKE k f) (map varE rs)) 0)

= (parseDefine rex err ok)
@ kids            | rexKids rex
@ [sigRex bodRex] | kids
@ sigRune         | rexRune sigRex
| if (neq 2 | len kids)
    | err rex "Expected two kids"
| if (eql "|" sigRune)
    : sym binds < readDestroyer sigRex err
    @ key       | rexIdnt (idx 0 | rexKids sigRex)
    | ok (key, sym, binds, bodRex)
| else
    : bind <- parseBinder sigRex err
    | ok (bind, bodRex)

!!=  ,, [%v 0 %x]
     ,, `3
  ^ parseDefine _ v2 id
  ` = x 3

!!=  ,, [%r [%v 0 %x],[%v 0 %y]]
     ,, `3
  ^ parseDefine _ v2 id
  ` = [x y] 3

;; TODO: Should there be a %f binder for the binding of a function?
;;
;;     That way these two cases would slot right together, and we must
;;     do something like that already with `@ (f x) ...` patterns?

!!=  ,, 0
     ,, %f
     ,, ([%v 0 %x], [%r ([%v 0 %y], [%v 0 %z])])
     ,, `y
  ^ parseDefine _ v2 id
  ` = (f x [y z]) y

= (unrollTis rex)
@ heir (rexHeir rex)
| if (isZero heir)            | ~[rex]
| if (neq (rexRune heir) "=") | ~[rex]
| CONS (rexSetHeir 0 rex)
| unrollTis heir

!!=  ~~ ` = x     3
     ~~ ` = (f x) 3
     ~~ ` = [x y]
          , 1 2
  | unrollTis
  ` = x 3
    = (f x) 3
    = [x y]
    , 1 2

; Input must be non-empty
= (mkDefsC a defBinds)
^ listFoldr _ (a, 0) defBinds
& (def [a acc])
| if (len def == 4)
    @ [key sym binds bodRex] def
    : a sig bod <- makeDestroyer a binds bodRex
    , a
    | OPEN 0 "#=" (sogKE key sym sig, bod) acc
| else
    @ [bind bodRex] def
    @ [a bSeq] (sequenceBinds a [[bind bodRex]])
    ^ foldr _ [a acc] bSeq
    & ([key var exp] [a acc])
    , a
    | OPEN 0 "#=" (keyKE key var, exp) acc

= ("=" s p e a r err ok)
@ defRexes | unrollTis r
: defBinds < ^ traverseList _ defRexes
             ? (step defRex ret)
             | parseDefine defRex err ret
@ a,r (mkDefsC a defBinds)
| ok p a r

= (weird [[[x]] %[=y a=z]]) [x y z]

!!= [3 9 8] (weird [[[3]] %[y=9 a=8]])

[x y]=[3 4]
[a b]=[3 4]

!!= x 3
!!= y 4
!!= a 3
!!= b 4

= [x y] [3 4]
= [a b] [3 4]

!!= x 3
!!= y 4
!!= a 3
!!= b 4

= x@[a b c] [9 8 7]

!!= x [9 8 7]
!!= a 9
!!= b 8
!!= c 7

= !p@[q r s] [9 8 7]

!!= p [9 8 7]
!!= q 9
!!= r 8
!!= s 7

= [n o _]@[_ _ w] [9 8 7]

!!= [n o w] [9 8 7]

!!= 1
    : ?(self x) <- (k & k 'self')
    | and (eql x 'self')
          (eql x lawName-self)

!!= 1
    : ??(self x) <- (k & k 'self')
    | and (eql x 'self')
          (| eql x | lawName | pinItem self)


;; TODO: consider removing the law-binding pattern: @ (f x) x ...

!!= 5
        @ @(PIN five)  (4 5)
        | five

!!= ('f', 1, 1)
        @ @(LAW n a b)  (f x ? x)
        [n a b]

!!= [6 5]
        @ @(CELL x y)  [5 6]
        [y x]


""" Exports """"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""

^-^
^-^ readDestroyer makeDestroyer
^-^
^-^ sequenceBinds bindSeqE
^-^
^-^ destructureRowE
^-^ destructureTabE  ;;  TODO: Don't export these, export a more general
^-^                  ;;  API instead.
^-^
^-^ "@" "?" "&" "="
